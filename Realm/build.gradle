plugins {
    id 'java-platform'
    id 'com.github.ben-manes.versions' version '0.39.0'
}

subprojects {

    tasks.withType(Test) {
        testLogging {
            events 'failed'
            exceptionFormat 'full'
            showCauses true
            showExceptions true
            showStackTraces true
        }
    }

    afterEvaluate {
        android {
            compileSdk 31

            defaultConfig {
                minSdk 26
                targetSdk 31

                testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
            }

            buildTypes {
                release {
                    signingConfig signingConfigs.debug
                }
            }

            flavorDimensions 'backend'

            productFlavors {
                memory
            }
        }

        configurations {
            testImplementation
        }

        dependencies {
            api platform(rootProject)
            testImplementation platform(rootProject)

            testImplementation 'junit:junit'
            testImplementation 'io.mockk:mockk'
            testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'
            testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test'
        }
    }
}

javaPlatform.allowDependencies()

dependencies {
    api platform('org.jetbrains.kotlinx:kotlinx-coroutines-bom:1.5.2')
    api platform('xyz.tynn.hoppa:bom:0.4.3')

    constraints {
        api 'androidx.core:core-ktx:1.7.0'

        api 'androidx.appcompat:appcompat:1.4.0'
        api 'com.google.android.material:material:1.4.0'
        api 'androidx.constraintlayout:constraintlayout:2.1.2'

        api 'androidx.startup:startup-runtime:1.1.0'

        api 'androidx.navigation:navigation-fragment-ktx:2.3.5'
        api 'androidx.navigation:navigation-ui-ktx:2.3.5'

        api 'junit:junit:4.13.2'
        runtime 'io.mockk:mockk:1.12.1'
        runtime 'androidx.test:rules:1.4.0'
        runtime 'androidx.test.ext:junit-ktx:1.1.3'
        runtime 'androidx.test.espresso:espresso-core:3.4.0'
        runtime 'org.jacoco:org.jacoco.ant:0.8.7'
    }
}

dependencyUpdates {
    def isNonStable = { version ->
        ['alpha', 'beta', 'dev', 'rc', 'cr', 'm', 'preview', 'b', 'ea', 'eap'].any {
            version ==~ /(?i).*[.-]${it}[.\d-+]*.*/
        }
    }
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
    gradleReleaseChannel = 'current'
}

wrapper {
    gradleVersion '7.3'
    distributionType Wrapper.DistributionType.ALL
}
