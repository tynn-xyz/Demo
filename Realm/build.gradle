plugins {
    id 'com.android.application' version '7.3.0' apply false
    id 'org.jetbrains.kotlin.android' version '1.7.10' apply false
    id 'com.github.ben-manes.versions' version '0.42.0'
    id 'java-platform'
}

subprojects {
    pluginManager.withPlugin('com.android.application') {
        apply from: "$rootDir/android.gradle"
        apply from: "$rootDir/testing.gradle"
    }

    pluginManager.withPlugin('com.android.library') {
        apply from: "$rootDir/android.gradle"
        apply from: "$rootDir/testing.gradle"
    }

    pluginManager.withPlugin('com.android.test') {
        apply from: "$rootDir/android.gradle"
    }

    tasks.withType(Test) {
        testLogging {
            events 'failed'
            exceptionFormat 'full'
            showCauses true
            showExceptions true
            showStackTraces true
        }
    }
}

javaPlatform.allowDependencies()

dependencies {
    api platform('org.jetbrains.kotlinx:kotlinx-coroutines-bom:1.6.4')
    api platform('xyz.tynn.hoppa:bom:0.4.4')

    constraints {
        api 'androidx.core:core-ktx:1.9.0'
        api 'androidx.startup:startup-runtime:1.1.1'

        api 'androidx.appcompat:appcompat:1.5.1'
        api 'com.google.android.material:material:1.6.1'
        api 'androidx.constraintlayout:constraintlayout:2.1.4'

        api 'androidx.navigation:navigation-fragment-ktx:2.5.2'
        api 'androidx.navigation:navigation-ui-ktx:2.5.2'

        runtime 'androidx.lifecycle:lifecycle-common:2.5.1'

        runtime 'junit:junit:4.13.2'
        runtime 'io.mockk:mockk:1.12.8'
        runtime 'androidx.test:rules:1.4.0'
        runtime 'androidx.test.ext:junit-ktx:1.1.3'
        runtime 'androidx.test.espresso:espresso-core:3.4.0'
    }
}

dependencyUpdates {
    def isNonStable = { version ->
        ['alpha', 'beta', 'dev', 'rc', 'cr', 'm', 'preview', 'b', 'ea', 'eap'].any {
            version ==~ /(?i).*[.-]${it}[.\d-+]*.*/
        }
    }
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
    gradleReleaseChannel = 'current'
}

wrapper {
    gradleVersion '7.5.1'
    distributionType Wrapper.DistributionType.ALL
}
