plugins {
    id 'com.github.ben-manes.versions' version '0.42.0'
    id 'com.android.application' version '7.3.0' apply false
    id 'org.jetbrains.kotlin.android' version '1.7.10' apply false
    id 'androidx.navigation.safeargs.kotlin' version '2.5.2' apply false
}

subprojects {
    plugins.withType(com.android.build.gradle.BasePlugin) {
        android {
            compileSdk 33

            defaultConfig {
                minSdk 21
                targetSdk 33

                testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
            }

            buildFeatures {
                buildConfig false
            }
        }

        configurations.all {
            exclude group: 'com.android.support'
        }
    }

    tasks.withType(Test) {
        testLogging {
            events "failed"
            exceptionFormat "full"
            showExceptions true
            showStackTraces true
            showCauses true
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

dependencyUpdates {
    def isNonStable = { String version ->
        ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'ea', 'eap'].any {
            version ==~ /(?i).*[.-]${it}[.\d-+]*.*/
        }
    }
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
    gradleReleaseChannel = 'current'
}

wrapper {
    gradleVersion '7.5.1'
    distributionType Wrapper.DistributionType.ALL
}
