plugins {
    id 'androidx.navigation.safeargs.kotlin' version '2.2.2' apply false
    id 'org.jetbrains.kotlin.android' version '1.3.72' apply false
    id 'com.android.application' version '3.6.3' apply false
    id 'com.github.ben-manes.versions' version '0.28.0'
    id 'xyz.tynn.idea.fix' version '0.1.2'
}

subprojects {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
        jcenter()
    }

    tasks.withType(Test) {
        testLogging {
            events "failed"
            exceptionFormat "full"
            showExceptions true
            showStackTraces true
            showCauses true
        }
    }

    plugins.withId('kotlin') {
        compileKotlin {
            kotlinOptions {
                jvmTarget = '1.8'
            }
        }

        compileTestKotlin {
            kotlinOptions {
                jvmTarget = '1.8'
            }
        }
    }

    plugins.withType(com.android.build.gradle.BasePlugin) {
        project.apply {
            plugin 'kotlin-android'
        }

        android {
            compileSdkVersion 29

            defaultConfig {
                minSdkVersion 21
                targetSdkVersion 29

                testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
            }

            compileOptions {
                sourceCompatibility '1.8'
                targetCompatibility '1.8'
            }

            kotlinOptions {
                jvmTarget = '1.8'
            }
        }

        configurations.all {
            exclude group: 'com.android.support'
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

dependencyUpdates {
    def isNonStable = { String version ->
        ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'ea', 'eap'].any {
            version ==~ /(?i).*[.-]${it}[.\d-+]*.*/
        }
    }
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
    gradleReleaseChannel = 'current'
}

wrapper {
    gradleVersion '6.4.1'
    distributionType Wrapper.DistributionType.ALL
}
